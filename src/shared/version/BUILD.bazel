# Copyright 2018- The Pixie Authors.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#
# SPDX-License-Identifier: Apache-2.0

load("//bazel:pl_build_system.bzl", "pl_cc_library")

package(default_visibility = ["//src:__subpackages__"])

# https://github.com/bazelbuild/bazel/issues/1992
# Borrowed from envoy build.
genrule(
    name = "gen_version_linkstamp",
    srcs = ["version_linkstamp.cc"],
    outs = ["gen_version_linkstamp.cc"],
    cmd = """
    STABLE_BUILD_SCM_REVISION=$$(
          grep STABLE_BUILD_SCM_REVISION bazel-out/stable-status.txt \\
        | sed 's/^STABLE_BUILD_SCM_REVISION //')
    STABLE_BUILD_SCM_STATUS=$$(
          grep STABLE_BUILD_SCM_STATUS bazel-out/stable-status.txt \\
        | sed 's/^STABLE_BUILD_SCM_STATUS //')
    BUILD_TIMESTAMP=$$(
          grep BUILD_TIMESTAMP bazel-out/volatile-status.txt \\
        | sed 's/^BUILD_TIMESTAMP //')
    STABLE_BUILD_TAG=$$(
          grep STABLE_BUILD_TAG bazel-out/stable-status.txt \\
        | sed 's/^STABLE_BUILD_TAG //')
    STABLE_BUILD_NUMBER=$$(
          grep STABLE_BUILD_NUMBER bazel-out/stable-status.txt \\
        | sed 's/^STABLE_BUILD_NUMBER //')

    echo "#define BUILD_SCM_REVISION \\"$$STABLE_BUILD_SCM_REVISION\\"" >> $@
    echo "#define BUILD_SCM_STATUS \\"$$STABLE_BUILD_SCM_STATUS\\"" >> $@
    echo "#define BUILD_TIMESTAMP $$BUILD_TIMESTAMP" >> $@
    echo "#define BUILD_TAG \\"$$STABLE_BUILD_TAG\\"" >> $@
    echo "#define BUILD_NUMBER \\"$$STABLE_BUILD_NUMBER\\"" >> $@
    cat $(location :version_linkstamp.cc) >> $@
    """,
    stamp = 1,
)

pl_cc_library(
    name = "cc_library",
    srcs = [
        "gen_version_linkstamp.cc",
        "version.cc",
    ],
    hdrs = ["version.h"],
    deps = [
        "//src/shared/types/typespb/wrapper:cc_library",
        "//third_party:arrow",
        "@com_google_absl//absl/time",
        "@com_google_farmhash//:farmhash",
    ],
)
