package(default_visibility = ["//visibility:public"])

load("@io_bazel_rules_go//go:def.bzl", "go_binary", "go_library")

cc_binary(
    name = "dummy_exe",
    srcs = ["dummy_exe.cc"],
    # This binary is only meant to be used to search for symbols,
    # So turn off all optimizations when building it.
    copts = ["-O0"],
    # This tells the linker to include a build-id, which is useful for locating external debug symbols.
    linkopts = [
        "-Wl,--build-id",
    ],
)

# To force no optimizations and no inlining, we need to pass -N -l,
# but we can't do this for debug builds because it would cause duplicate flags
# (i.e. the one we pass and the one that go_rules adds). The go compiler doesn't
# handle the duplicate flags well, and silently causes the inlining to be disabled.
gcflags_no_opt_no_inline = select({
    "@pl//bazel:debug_build": [],
    "//conditions:default": [
        "-N",
        "-l",
    ],
})

go_library(
    name = "go_default_library",
    srcs = ["dummy_go_binary.go"],
    gc_goopts = gcflags_no_opt_no_inline,
    importpath = "pixielabs.ai/pixielabs/src/stirling/obj_tools/testdata",
)

go_binary(
    name = "dummy_go_binary",
    embed = [":go_default_library"],
)

filegroup(
    name = "stripped_exe",
    srcs = [
        "stripped_dummy_exe",
        "usr/lib/debug/.build-id/7d/eb0e3f89deba61.debug",
    ],
)

filegroup(
    name = "prebuilt_exe",
    srcs = ["prebuilt_dummy_exe"],
)

#------------------------------------------------------------------------------

# While the following rules are included for documentation purposes,
# the output of these command are directly checked into the repo.
# This is because we need a consistent build-id for testing purposes.

#genrule(
#    name = "dummy_exe_stripped",
#    srcs = [":dummy_exe"],
#    outs = [
#        "stripped_dummy_exe",
#    ],
#    cmd = "objcopy -S \"$<\" \"$@\"",
#)
#
#genrule(
#    name = "dummy_exe_debug",
#    srcs = [":dummy_exe"],
#    outs = [
#        "dummy_exe.debug",
#    ],
#    cmd = "objcopy --only-keep-debug \"$<\" \"$@\"",
#)

#------------------------------------------------------------------------------
