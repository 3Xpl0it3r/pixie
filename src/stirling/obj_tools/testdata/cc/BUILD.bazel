# Copyright 2018- The Pixie Authors.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#
# SPDX-License-Identifier: Apache-2.0

load("@rules_cc//cc:defs.bzl", "cc_library")

package(default_visibility = ["//src/stirling:__subpackages__"])

# We build dummy_exe with clang++ regardless of the bazel config.
# This is test data, so we want deterministic debug symbols.
genrule(
    name = "dummy_cc_binary",
    srcs = ["dummy_exe.cc"],
    outs = ["dummy_exe"],
    # -O0: Disables optimization which in turn disables inlining.
    # -g: Produces dwarf information.
    # -Wl,--build-id: This tells the linker to include a build-id,
    #                 which is useful for locating external debug symbols.
    cmd = "clang++ -O0 -g -Wl,--build-id -o $@ $<",
)

cc_library(
    name = "dummy_exe_fixture",
    hdrs = ["dummy_exe_fixture.h"],
    data = [":dummy_cc_binary"],
    deps = ["//src/common/exec:cc_library"],
)

filegroup(
    name = "stripped_exe",
    srcs = [
        "stripped_dummy_exe",
        "usr/lib/debug/.build-id/7d/eb0e3f89deba61.debug",
    ],
)

filegroup(
    name = "prebuilt_exe",
    srcs = [
        "prebuilt_dummy_exe",
    ],
)

#------------------------------------------------------------------------------

# The output of dummy_exe_stripped_target is also checked into the repo as stripped_dummy_exe.
# This is because we need a consistent build-id for testing purposes.

genrule(
    name = "dummy_exe_stripped_target",
    srcs = [":dummy_exe"],
    outs = [
        "dummy_exe_stripped",
    ],
    cmd = "objcopy --strip-all $(location :dummy_exe) $(location dummy_exe_stripped)",
)

genrule(
    name = "dummy_exe_debug_target",
    srcs = [":dummy_exe"],
    outs = [
        "dummy_exe.debug",
    ],
    cmd = "objcopy --only-keep-debug $(location :dummy_exe) $(location dummy_exe.debug)",
)

genrule(
    name = "dummy_exe_debuglink_target",
    srcs = [
        ":dummy_exe",
        ":dummy_exe_debug_target",
    ],
    outs = [
        "dummy_exe_debuglink",
    ],
    cmd = "objcopy --strip-all --add-gnu-debuglink=$(location :dummy_exe_debug_target) $(location :dummy_exe) $(location dummy_exe_debuglink)",
)
